apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "org.openrct2.android"
        minSdkVersion 16
        targetSdkVersion 25

        versionCode 2
        versionName "0.0.5"

        externalNativeBuild {
            cmake {
                arguments '-DDEBUG_LEVEL=3'//, '-DCMAKE_BUILD_TYPE=RELWITHDEBINFO'
                //abiFilters 'x86', 'x86_64'
                // abiFilters 'armeabi-v7a', 'arm64-v8a'
                abiFilters 'armeabi-v7a'
                targets 'openrct2'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        cmake {
            path 'src/main/CMakeLists.txt'
        }
    }
}

apply plugin: 'de.undercouch.download'


def downloadLibrarySources(name, url, md5, archiveRoot) {
    tasks.create(name: "downloadLibraries:${name}") {
        def tempFile = new File(buildDir, "libs/${md5}.tar.gz")
        def destDir = new File(projectDir, "/../libs/${name}")
        outputs.dir destDir

        doLast {
            if (!tempFile.exists()) {
                download {
                    src url
                    dest tempFile
                    overwrite true
                }
            }

            copy {
                from tarTree(resources.gzip(tempFile))
                into temporaryDir
            }

            copy {
                from new File(temporaryDir, archiveRoot)
                into destDir
            }
        }
    }
}

task downloadLibraries {
    downloadLibrarySources('sdl-ttf','http://www.libsdl.org/projects/SDL_ttf/release/SDL2_ttf-2.0.14.tar.gz' ,'34c5fdc8508c7e14356579477f9ca2b0d6c06ffc','SDL2_ttf-2.0.14')

    dependsOn {
        tasks.findAll { task -> task.name.startsWith('downloadLibraries:') }
    }
}

dependencies {
    compile 'commons-io:commons-io:2.5'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.google.android.gms:play-services-analytics:9.8.0'
}

task copyLanguageAssets(type: Copy) {
    from '../../../data'
    into 'src/main/assets/data'
}

project.afterEvaluate {
    mergeDebugAssets.dependsOn copyLanguageAssets

    mergeReleaseAssets.dependsOn copyLanguageAssets
}

project.afterEvaluate {
    // Download the licenses when (actually: just before) the
    // resources are generated (for both the "debug" and the
    // "release" build types).

    // If you add/change build types, you have to add to/change
    // these task names.
    preBuild.dependsOn tasks['downloadLibraries']
    preDebugBuild.dependsOn tasks['downloadLibraries']
}

apply plugin: 'com.google.gms.google-services'
